class NTM_Zombieman : NTM_Enemy replaces Zombieman {
	Default {
		Health 20;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		ActiveSound "grunt/active";
		Obituary "$OB_ZOMBIE";
		Tag "$FN_ZOMBIE";
		DropItem "Clip";
		Species "Zombieman";
		NTM_Enemy.TicsPerChase 3;
	}
	
	States {
		Spawn:
			NZOM A 10 A_Look;
			Loop;
		
		DukeSee:
			NZOM ABCD 6 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NZOM AABBCCDD 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NZOM A 1 A_NTM_SWChase;
			NZOM AA 1 A_NTM_SWChase2;
			NZOM A 1 A_NTM_SWChase;
			NZOM AA 1 A_NTM_SWChase2;
			NZOM B 1 A_NTM_SWChase;
			NZOM BB 1 A_NTM_SWChase2;
			NZOM B 1 A_NTM_SWChase;
			NZOM BB 1 A_NTM_SWChase2;
			NZOM C 1 A_NTM_SWChase;
			NZOM CC 1 A_NTM_SWChase2;
			NZOM C 1 A_NTM_SWChase;
			NZOM CC 1 A_NTM_SWChase2;
			NZOM D 1 A_NTM_SWChase;
			NZOM DD 1 A_NTM_SWChase2;
			NZOM D 1 A_NTM_SWChase;
			NZOM DD 1 A_NTM_SWChase2;
			Loop;
		
		Missile:
			NZOM E 0 {
				if (sv_ntm_grenadiers) {
					int skill = G_SkillPropertyInt(7);
					int ran = Random(0, 256);
					
					if ((skill >= 4 && ran <= 20) || (skill >= 2 && ran <= 10)) {
						double dist = Distance3D(target);
						
						if (dist > 256 && dist < 520) {
							return FindState("Grenade");
						}
					}
				}
				
				return FindState("Missile2");
			}
			
			Stop;
		
		Missile2:
			NZOM E 10 A_FaceTarget;
			NZOM F 2 Bright A_NTM_PosAttack;
			NZOM E 3 A_FaceTarget;
			NZOM F 2 Bright A_NTM_PosAttack;
			NZOM E 3 A_FaceTarget;
			NZOM F 2 Bright A_NTM_PosAttack;
			NZOM E 8;
			Goto See;
		
		Grenade:
			NZOM V 0 A_JumpIfCloser(960, 1, true);
			Goto Missile;
			NZOM V 6 A_FaceTarget;
			
			NZOM W 6 {
				if (CheckSight(target)) {
					A_FaceTarget();
				}
			}
			
			NZOM X 6 {
				double dist = 1;
				double zDist = 1;
				
				if (target) {
					dist = Distance2D(target) / 520;
					zDist += (((target.pos.z - pos.z) * 1.6) / 260);
				}
				
				A_ThrowGrenade("NTM_Grenade", 10, FRandom(9, 11) * dist, FRandom(4, 6) * zDist);
			}
			
			NZOM Y 6;
			Goto See;
		
		Pain:
			NZOM G 6 A_Pain;
			NZOM G 0 A_JumpIf(health <= 10 && Random(0, 1), "Duck");
			Goto See;
		
		Duck:
			NZOM S Random(10, 30) A_NTM_PainDisable;
			NZOM T 10 A_NTM_PosDuck;
			NZOM U 2 Bright A_NTM_PosAttack;
			NZOM T 3 A_FaceTarget;
			NZOM U 2 Bright A_NTM_PosAttack;
			NZOM T 3 A_FaceTarget;
			NZOM U 2 Bright A_NTM_PosAttack;
			NZOM T 8;
			NZOM T 0 A_NTM_PainEnable;
			Goto See;
		
		Death:
			NZOM H 1 A_NTM_PreDeath;
			NZOM H 4 A_NTM_Death;
			NZOM I 5 A_NoBlocking;
			NZOM JK 5;
			NZOM L -1;
			Stop;
		
		XDeath:
			NMGI A 1 A_NTM_PreDeath;
			NMGI A 4 A_NTM_XDeath;
			NMGI B 5 A_NoBlocking;
			NMGI CDEFG 5;
			NMGI H -1;
			Stop;
		
		Raise:
			NZOM K 0 A_NTM_PainEnable;
			NZOM KJIH 5;
			Goto See;
	}
	
	action void A_NTM_PosAttack() {
		A_CustomBulletAttack(30, 0, 1, Random(2, 6), "BulletPuff", 0, CBAF_NoRandom);
	}
	
	action void A_NTM_PosDuck() {
		if (Random(0, 1)) {
			SetStateLabel("See");
		}
		
		A_FaceTarget();
	}
}

class NTM_Grenade : Actor {
	Default {
		Radius 4;
		Height 4;
		Scale 0.3;
		Speed 20;
		Damage 25;
		Gravity 0.5;
		BounceSound "NTM_Grenade/bounce";
		BounceType "Hexen";
		Projectile;
		
		+CanBounceWater
		-NoGravity
		-NoTeleport
		+DontBlast
	}
	
	States {
		Spawn:
			NGRE ABCDEFABCDEFABCDEF 3;
		Death:
			NGRE G 0 {
				A_Explode();
				A_StartSound("world/barrelx", 5, 0, 1, Attn_Norm, FRandom(0.8, 1.2));
				A_SpawnItemEx("NTM_Explosion2");
			}
			
			TNT1 A 15 A_Quake(2, 15, 0, 800, "");
			Stop;
	}
}