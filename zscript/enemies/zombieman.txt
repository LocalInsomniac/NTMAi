class NTM_Zombieman : NTM_Humanoid {
	Default {
		Health 20;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		SeeSound "grunt/sight";
		AttackSound "grunt/attack";
		PainSound "grunt/pain";
		DeathSound "grunt/death";
		NTM_Humanoid.BurnSound "grunt/burn";
		ActiveSound "grunt/active";
		Obituary "$OB_ZOMBIE";
		Tag "$FN_ZOMBIE";
		DropItem "Clip";
		Species "Zombieman";
		NTM_Enemy.TicsPerChase 3;
	}
	
	States {
		Spawn:
			NZOM A 10 A_Look;
			Loop;
		
		DukeSee:
			NZOM AABBCCDD 3 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NZOM AABBCCDD 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NZOM A 1 A_NTM_SWChase;
			NZOM AA 1 A_NTM_SWChase2;
			NZOM A 1 A_NTM_SWChase;
			NZOM AA 1 A_NTM_SWChase2;
			NZOM B 1 A_NTM_SWChase;
			NZOM BB 1 A_NTM_SWChase2;
			NZOM B 1 A_NTM_SWChase;
			NZOM BB 1 A_NTM_SWChase2;
			NZOM C 1 A_NTM_SWChase;
			NZOM CC 1 A_NTM_SWChase2;
			NZOM C 1 A_NTM_SWChase;
			NZOM CC 1 A_NTM_SWChase2;
			NZOM D 1 A_NTM_SWChase;
			NZOM DD 1 A_NTM_SWChase2;
			NZOM D 1 A_NTM_SWChase;
			NZOM DD 1 A_NTM_SWChase2;
			Loop;
		
		Missile:
			NZOM E 0 {
				if (sv_ntm_grenadiers) {
					int skill = G_SkillPropertyInt(7);
					int ran = Random(0, 256);
					
					if ((skill >= 3 && ran <= 20) || (skill >= 1 && ran <= 10)) {
						double dist = Distance3D(target);
						
						if (dist > 256 && dist < 520) {
							return FindState("Grenade");
						}
					}
				}
				
				return FindState("Missile2");
			}
			
			Stop;
		
		Missile2:
			NZOM E 10 A_FaceTarget;
			NZOM F 2 Bright A_NTM_PosAttack;
			NZOM E 3 A_FaceTarget;
			NZOM F 2 Bright A_NTM_PosAttack;
			NZOM E 3 A_FaceTarget;
			NZOM F 2 Bright A_NTM_PosAttack;
			NZOM E 8;
			Goto See;
		
		Grenade:
			NZOM P 0 A_JumpIfCloser(960, 1, true);
			Goto Missile;
			NZOM P 6 A_FaceTarget;
			
			NZOM Q 6 {
				if (CheckSight(target)) {
					A_FaceTarget();
				}
			}
			
			NZOM R 6 {
				double dist = 1;
				double zDist = 1;
				
				if (target) {
					dist = Distance2D(target) / 520;
					zDist += (((target.pos.z - pos.z) * 1.6) / 260);
				}
				
				A_ThrowGrenade("NTM_Grenade", 10, FRandom(9, 11) * dist, FRandom(4, 6) * zDist);
			}
			
			NZOM S 6;
			Goto See;
		
		Pain:
			NZOM G 6 A_Pain;
			NZOM G 0 A_JumpIf(health <= 10 && Random(0, 1), "Duck");
			Goto See;
		
		Duck:
			NZOM M Random(10, 30) A_NTM_PainDisable;
			NZOM N 10 A_NTM_PosDuck;
			NZOM O 2 Bright A_NTM_PosAttack;
			NZOM N 3 A_FaceTarget;
			NZOM O 2 Bright A_NTM_PosAttack;
			NZOM N 3 A_FaceTarget;
			NZOM O 2 Bright A_NTM_PosAttack;
			NZOM N 8;
			NZOM N 0 A_NTM_PainEnable;
			Goto See;
		
		Death:
			NZOM H 1 A_NTM_PreDeath;
			NZOM H 4 A_NTM_Death;
			NZOM I 5 A_NoBlocking;
			NZOM JK 5;
			NZOM L -1;
			Stop;
		
		Death.Chainsaw:
			NZOM T 10 {
				A_NTM_PreDeath();
				A_Scream();
				bShootable = false;
				
				DropItem drop = GetDropItems();
				
				while (drop != null) {
					A_DropItem(drop.name, drop.amount);
					drop = drop.next;
				}
			}
			
			NZOM T 15 {
				height = GetDeathHeight();
				bCorpse = true;
			}
			
			NZOM U 5 {
				for (int i = 0; i < 3; ++i) {
					A_SpawnItemEx("NTM_FlyingBlood", 8, 4, 28, FRandom(2, 5), FRandom(-5, -2), FRandom(2, 6));
				}
				
				A_StartSound("NTM_Chainsaw/slash", Chan_5, ChanF_Overlap);
				A_NoBlocking(false);
			}
			
			NZOM VWXYZ[^ 5;
			NZOM ] -1;
			Stop;
		
		XDeath:
		XDeath.Chainsaw:
			NMGI A 1 A_NTM_PreDeath;
			NMGI A 4 A_NTM_XDeath;
			NMGI B 5 A_NoBlocking;
			NMGI CDEFG 5;
			NMGI H -1;
			Stop;
		
		Raise:
			NZOM KJIH 5;
			Goto See;
	}
	
	action void A_NTM_PosAttack() {
		A_CustomBulletAttack(30, 0, 1, Random(2, 6), "BulletPuff", 0, CBAF_NoRandom);
	}
	
	action void A_NTM_PosDuck() {
		if (Random(0, 1)) {
			SetStateLabel("See");
		}
		
		A_FaceTarget();
	}
}