class NTM_ChaingunGuy : NTM_Enemy {
	bool fired;
	
	Default {
		Health 70;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 170;
		SeeSound "chainguy/sight";
		PainSound "chainguy/pain";
		DeathSound "chainguy/death";
		ActiveSound "chainguy/active";
		AttackSound "chainguy/attack";
		Obituary "$OB_CHAINGUY";
		Tag "$FN_HEAVY";
		DropItem "Chaingun";
		Species "ChaingunGuy";
		NTM_Enemy.TicsPerChase 3;
	}
	
	States {
		Spawn:
			NCGZ A 10 A_Look;
			Loop;
		
		See:
			#### # 0 {
				fired = false;
			}
			
			Goto Super::See;
		
		DukeSee:
			NCGZ AABBCCDD 3 A_Chase;
			Loop;
		
		BloodSee:
			NCGZ AABBCCDD 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NCGZ A 1 A_NTM_SWChase;
			NCGZ AA 1 A_NTM_SWChase2;
			NCGZ A 1 A_NTM_SWChase;
			NCGZ AA 1 A_NTM_SWChase2;
			NCGZ B 1 A_NTM_SWChase;
			NCGZ BB 1 A_NTM_SWChase2;
			NCGZ B 1 A_NTM_SWChase;
			NCGZ BB 1 A_NTM_SWChase2;
			NCGZ C 1 A_NTM_SWChase;
			NCGZ CC 1 A_NTM_SWChase2;
			NCGZ C 1 A_NTM_SWChase;
			NCGZ CC 1 A_NTM_SWChase2;
			NCGZ D 1 A_NTM_SWChase;
			NCGZ DD 1 A_NTM_SWChase2;
			NCGZ D 1 A_NTM_SWChase;
			NCGZ DD 1 A_NTM_SWChase2;
			Loop;
		
		Missile:
			NCGZ E 10 {
				A_FaceTarget();
				fired = true;
			}
			
			NCGZ F 2 Bright A_CPosAttack;
			NCGZ E 2 A_FaceTarget;
			NCGZ F 2 Bright A_CPosAttack;
			NCGZ E 2 A_CPosRefire;
			Goto Missile + 1;
		
		Pain:
			NCGZ G 2 A_Pain;
			
			NCGZ G 4 {
				fired = false;
			}
			
			Goto See;
		
		Death:
			#### # 0 A_JumpIf(fired && Random(0, 1), "Death2");
			NCGZ H 1 A_NTM_PreDeath;
			NCGZ H 4 A_NTM_Death;
			NCGZ I 5 A_NoBlocking;
			NCGZ JK 5;
			NCGZ L -1;
			Stop;
		
		Death2:
			NCGZ M 1 A_NTM_PreDeath;
			NCGZ M 2 A_NTM_Death;
			NCGZ N 3 Bright A_StartSound("chainguy/attack", Chan_Weapon);
			NCGZ H 3;
			NCGZ O 3 Bright A_StartSound("chainguy/attack", Chan_Weapon);
			NCGZ I 3;
			NCGZ P 3 Bright A_StartSound("chainguy/attack", Chan_Weapon);
			NCGZ J 3;
			NCGZ Q 3 Bright A_StartSound("chainguy/attack", Chan_Weapon);
			NCGZ K 3;
			NCGZ R 3 Bright A_StartSound("chainguy/attack", Chan_Weapon);
			NCGZ L -1 A_NoBlocking;
			Stop;
		
		XDeath:
			NMGI A 1 A_NTM_PreDeath;
			NMGI A 4 A_NTM_XDeath;
			NMGI B 5 A_NoBlocking;
			NMGI CDEFG 5;
			NMGI H -1;
			Stop;
		
		Raise:
			NCGZ KJIH 5;
			Goto See;
	}
}