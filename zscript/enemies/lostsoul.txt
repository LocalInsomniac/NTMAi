class NTM_LostSoul : NTM_Enemy {
	Default {
		Health 60;
		Radius 16;
		Height 56;
		Mass 50;
		Speed 4;
		Damage 3;
		PainChance 256;
		MinMissileChance 160;
		AttackSound "skull/melee";
		PainSound "skull/pain";
		DeathSound "skull/death";
		ActiveSound "skull/active";
		Obituary "$OB_SKULL";
		Tag "$FN_LOST";
		Species "LostSoul";
		NTM_Enemy.TicsPerChase 3;
		
		-FloorClip
		+Float
		+NoGravity
		MissileChanceMult 0.0625;
		+DontFall
		+NoIceDeath
		+OnlySlamSolid
		+NoBlood
		+Bright
	}
	
	States {
		Spawn:
			NLOS AB 6 A_Look;
			Goto Spawn;
		
		See:
			NLOS A 0 A_JumpIf(bSkullFly, "Missile2");
			Goto Super::See;
			
		DukeSee:
			NLOS AABB 3 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NLOS AABB 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NLOS A 1 A_NTM_SWChase;
			NLOS AA 1 A_NTM_SWChase2;
			NLOS A 1 A_NTM_SWChase;
			NLOS AA 1 A_NTM_SWChase2;
			NLOS B 1 A_NTM_SWChase;
			NLOS BB 1 A_NTM_SWChase2;
			NLOS B 1 A_NTM_SWChase;
			NLOS BB 1 A_NTM_SWChase2;
			Loop;
		
		Missile:
			NLOS CD 4 A_FaceTarget;
			NLOS C 2 A_FaceTarget;
			NLOS C 2 A_SkullAttack;
		Missile2:
			NLOS D 4 A_JumpIf(vel.xy.Length() < 10, 2);
			NLOS C 4;
			Loop;
			
			NLOS D 0 {
				bSkullFly = false;
			}
			
			Goto See;
			
		Pain:
			NLOS E 6 A_Pain;
			Goto See;
			
		Death:
			NLOS E 1;
			
			NLOS E 0 {
				Actor boom = Spawn("NTM_Explosion", pos + (0, 0, 16));
				
				if (boom != null) {
					boom.scale *= 0.8;
				}
				
				A_SpawnDebris("NTM_LostSoulGib", false, 1.5);
				A_NTM_Death();
			}
			
			Stop;
	}
}

class NTM_LostSoulGib : NTM_Gib {
	Default {
		Scale 0.5;
		
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			NLOX A 0 NoDelay {
				bXFlip = Random(0, 1);
				bYFlip = Random(0, 1);
			}
			
			NLOX ABCDEFGH 3;
			Loop;
	}
}