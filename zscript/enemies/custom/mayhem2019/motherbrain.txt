class NTM_MotherBrain : NTM_Boss {
	Default {
		Health 1500;
		Speed 9;
		Radius 48;
		Height 100;
		Mass 1000;
		PainChance 40;
		SeeSound "NTM_MotherBrain/sight";
		ActiveSound "NTM_MotherBrain/active";
		PainSound "NTM_MotherBrain/pain";
		DeathSound "NTM_MotherBrain/death";
		NTM_Enemy.TicsPerChase 3;
		
		+NoGravity
		+Float
		+FloatBob
		-FloorClip
	}
	
	States {
		Spawn:
			NMOB A 10 A_Look;
			Loop;
		
		DukeSee:
			NMOB A 3 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NMOB A 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NMOB A 1 A_NTM_SWChase;
			NMOB AA 1 A_NTM_SWChase2;
			Loop;
		
		Missile:
			NMOB BC 4 A_FaceTarget;
			
			NMOB D 4 {
				A_FaceTarget();
				
				for (double i = -45; i <= 45; i += 90) {
					A_SpawnProjectile("NTM_MotherBrainBall", 56, 0, i);
				}
			}
		
		Missile2:
			NMOB E 3 Bright {
				A_FaceTarget();
				
				for (double i = -90; i <= 90; i += 180) {
					Actor missile = A_SpawnProjectile("NTM_ArachnorbBall", 72);
					
					if (missile) {
						double ang = angle - i;
						
						missile.SetOrigin(missile.pos + (Cos(ang) * 10, Sin(ang) * 10, 0), false);
					}
				}
			}
			
			NMOB B 4 A_FaceTarget;
			NMOB B 0 A_SpidRefire;
			Goto Missile2;
		
		Pain:
			NMOB F 14 A_Pain;
			Goto See;
		
		Death:
			NMOB G 8 {
				bFloatBob = false;
				A_NTM_Death();
			}
			
			NMOB H 8 A_NoBlocking;
			NMOB I 8 A_SetFloorClip;
			NMOB JKL 8;
			NMOB M -1;
			Stop;
	}
}

class NTM_MotherBrainBall : NTM_ArachnotronBall {
	Default {
		Speed 12;
		Scale 0.625;
		SeeSound "NTM_MotherBrain/attack";
		DeathSound "NTM_MotherBrain/ballx";
		Translation "NTM_LightningOrb_MotherBrain";
		
		+SeekerMissile
	}
	
	States {
		Spawn:
			NORB ABCD 1 A_Tracer2(2);
			Loop;
			
		Death:
			TNT1 A 15 {
				Actor boom = Spawn("NTM_Explosion4", pos + (0, 0, 15));
				
				if (boom != null) {
					boom.A_SetTranslation("NTM_Explosion_MotherBrain");
				}
				
				NTMAi.Explode(self);
				A_Quake(2, 15, 0, 512, "");
			}
			
			Stop;
	}
}