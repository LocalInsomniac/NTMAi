class NTM_Zombie : NTM_Humanoid {
	Default {
		Health 20;
		Radius 20;
		Height 56;
		Speed 8;
		PainChance 200;
		SeeSound "NTM_Zombie/sight";
		ActiveSound "NTM_Zombie/active";
		PainSound "NTM_Zombie/pain";
		DeathSound "NTM_Zombie/death";
		NTM_Humanoid.BurnSound "NTM_Zombie/burn";
		HitObituary "%o was eaten by a Zombie.";
		NTM_Enemy.TicsPerChase 3;
		Species "Zombieman";
		Tag "Zombie";
	}
	
	States {
		Spawn:
			NZO2 B 10 A_Look;
			Loop;
		
		DukeSee:
			NZO2 AABBCCDD 3 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NZO2 AABBCCDD 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NZO2 A 1 A_NTM_SWChase;
			NZO2 AA 1 A_NTM_SWChase2;
			NZO2 A 1 A_NTM_SWChase;
			NZO2 AA 1 A_NTM_SWChase2;
			NZO2 B 1 A_NTM_SWChase;
			NZO2 BB 1 A_NTM_SWChase2;
			NZO2 B 1 A_NTM_SWChase;
			NZO2 BB 1 A_NTM_SWChase2;
			NZO2 C 1 A_NTM_SWChase;
			NZO2 CC 1 A_NTM_SWChase2;
			NZO2 C 1 A_NTM_SWChase;
			NZO2 CC 1 A_NTM_SWChase2;
			NZO2 D 1 A_NTM_SWChase;
			NZO2 DD 1 A_NTM_SWChase2;
			NZO2 D 1 A_NTM_SWChase;
			NZO2 DD 1 A_NTM_SWChase2;
			Loop;
		
		Melee:
			NZO2 A 0 A_Jump(128, "Melee2");
			NZO2 E 6 A_FaceTarget;
			NZO2 F 6 A_NTM_ZombieSwing;
			NZO2 G 6 A_NTM_ZombieAttack;
			Goto See;
		
		Melee2:
			NZO2 H 6 A_FaceTarget;
			NZO2 I 6 A_NTM_ZombieSwing;
			NZO2 J 6 A_NTM_ZombieAttack;
			Goto See;
		
		Pain:
			NZO2 K 6 A_Pain;
			Goto See;
		
		Death:
			NZO2 A 0 A_Jump(32, "Death2");
			NZO2 L 6 A_NTM_Death;
			NZO2 M 5 A_NoBlocking;
			NZO2 N 6;
			NZO2 O 5;
			NZO2 P -1;
			Stop;
			
		Death2:
			NZO2 Q 0 A_NTM_ZombieDeath;
			NZO2 Q 6 A_NTM_Death;
			NZO2 R 5 A_NoBlocking;
			NZO2 S 6;
			NZO2 T 5;
			NZO2 U -1;
			Stop;
		
		XDeath:
		XDeath.Fire:
			NZO2 V 0 A_NTM_ZombieDeath;
			NZO2 V 5 A_NTM_XDeath;
			NZO2 W 5 A_NoBlocking;
			NZO2 XYZ 5;
			NZO2 [ -1;
			Stop;
		
		Raise:
			NZO2 ONML 6;
			Goto See;
	}
	
	action void A_NTM_ZombieDeath() {
		A_SpawnItemEx("NTM_ZombieHead", 0, 0, height, FRandom(-5, 5), FRandom(-5, 5), FRandom(10, 12));
	}
	
	action void A_NTM_ZombieSwing() {
		A_StartSound("NTM_Zombie/attack");
		A_FaceTarget();
	}
	
	action void A_NTM_ZombieAttack() {
		A_CustomMeleeAttack(Random(1, 5) * 3, "NTM_Zombie/melee");
	}
}

class NTM_ZombieHead : NTM_SkeletonSkull {
	Default {
		Scale 0.5;
		BounceSound "NTM_FlyingGib/splat";
	}
	
	States {
		Spawn:
			NZHE ABCDEFGH 2 A_SpawnItemEx("NTM_BloodTrail");
			Loop;
			
		Bounce:
			NZHE A 0 A_SpawnItemEx("NTM_FlyingBlood");
			Goto Spawn;
		
		Death:
			NZHE I 0 A_QueueCorpse;
			NZHE I -1 A_SetFloorClip;
			Stop;
	}
}