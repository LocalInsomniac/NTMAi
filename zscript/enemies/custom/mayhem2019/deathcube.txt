class NTM_DeathCube : NTM_Enemy {
	Default {
		Health 520;
		Speed 0;
		Radius 16;
		Height 32;
		PainChance 50;
		ActiveSound "NTM_DeathCube/active";
		DeathSound "NTM_DeathCube/death";
		
		+DontThrust
		+Float
		+LookAllAround
		+NoBlood
		+NoGravity
		-FloorClip
	}
	
	States {
		Spawn:
			NDCU ABC 4 A_Look;
			Loop;
		
		See:
			NDCU A 0 A_StartSound(activeSound, Chan_5, ChanF_Looping);
			NDCU ABC 4 A_Chase;
			Goto See + 1;
		
		Missile:
			NDCU D 10 A_FaceTarget;
			NDCU EF 4 Bright A_FaceTarget;
			NDCU F 0 A_SpawnProjectile("NTM_ArachnotronBall", 16);
			NDCU F 0 A_SpidRefire;
			Goto Missile + 1;
		
		Pain:
			NDCU A 0;
			Goto See + 1;
		
		Death:
			NDCU G 2 A_NTM_Death;
			
			TNT1 A 15 {
				Spawn("NTM_Explosion2", pos + (0, 0, 10));
				NTMAi.Explode(self);
				A_Quake(2, 15, 0, 512, "");
				A_SpawnDebris("NTM_LostSoulGib", false, 1.5);
			}
			
			Stop;
	}
	
	override void PostBeginPlay() {
		super.PostBeginPlay();
		
		AddZ(32, false);
	}
	
	override void Die(Actor source, Actor inflictor, int dmgFlags, Name meansOfDeath) {
		super.Die(source, inflictor, dmgFlags, meansOfDeath);
		
		A_StopSound(Chan_5);
	}
}