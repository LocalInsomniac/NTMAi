class NTM_Cyberdemon : NTM_Boss {
	Default {
		Health 4000;
		Radius 40;
		Height 110;
		Mass 1000;
		Speed 16;
		PainChance 20;
		MinMissileChance 160;
		SeeSound "cyber/sight";
		ActiveSound "cyber/active";
		PainSound "cyber/pain";
		DeathSound "cyber/death";
		Obituary "$OB_CYBORG";
		Tag "$FN_CYBER";
		Species "Cyberdemon";
		NTM_Enemy.TicsPerChase 3;
		
		+E2M8Boss
		+E4M6Boss
	}
	
	States {
		Spawn:
			NCYB A 10 A_Look;
			Loop;
		
		DukeSee:
			NCYB A 0 A_NTM_CyberHoof;
			NCYB AABB 3 A_NTM_DukeChase;
			NCYB C 0 A_NTM_CyberMetal;
			NCYB CCDD 3 A_NTM_DukeChase;
			Loop;
		
		BloodSee:
			NCYB A 0 A_NTM_CyberHoof;
			NCYB AABB 3 A_NTM_BloodChase;
			NCYB C 0 A_NTM_CyberMetal;
			NCYB CCDD 3 A_NTM_BloodChase;
			Loop;
		
		SWSee:
			NCYB A 0 A_NTM_CyberHoof;
			NCYB A 1 A_NTM_SWChase;
			NCYB AA 1 A_NTM_SWChase2;
			NCYB A 1 A_NTM_SWChase;
			NCYB AA 1 A_NTM_SWChase2;
			NCYB B 1 A_NTM_SWChase;
			NCYB BB 1 A_NTM_SWChase2;
			NCYB B 1 A_NTM_SWChase;
			NCYB BB 1 A_NTM_SWChase2;
			NCYB C 0 A_NTM_CyberMetal;
			NCYB C 1 A_NTM_SWChase;
			NCYB CC 1 A_NTM_SWChase2;
			NCYB C 1 A_NTM_SWChase;
			NCYB CC 1 A_NTM_SWChase2;
			NCYB D 1 A_NTM_SWChase;
			NCYB DD 1 A_NTM_SWChase2;
			NCYB D 1 A_NTM_SWChase;
			NCYB DD 1 A_NTM_SWChase2;
			Loop;
		
		Missile:
			NCYB E 0 A_Jump(100, "Missile2");
			NCYB E 6 A_FaceTarget;
			NCYB F 6 Bright A_NTM_CyberAttack;
			NCYB E 18 A_FaceTarget;
			NCYB F 6 Bright A_NTM_CyberAttack;
			NCYB E 18 A_FaceTarget;
			NCYB F 6 Bright A_NTM_CyberAttack;
			NCYB E 6 A_FaceTarget;
			Goto See;
		
		Missile2:
			NCYB G 10 A_FaceTarget;
			NCYB H 3 Bright A_NTM_CyberAttack2;
			NCYB G 3 A_FaceTarget;
			NCYB H 3 Bright A_NTM_CyberAttack2;
			NCYB G 3 A_FaceTarget;
			NCYB H 3 Bright A_NTM_CyberAttack2;
			NCYB G 3 A_FaceTarget;
			NCYB H 3 Bright A_NTM_CyberAttack2;
			NCYB G 3 A_FaceTarget;
			NCYB H 3 Bright A_NTM_CyberAttack2;
			NCYB G 8 A_FaceTarget;
			Goto See;
		
		Pain:
			NCYB I 10 A_Pain;
			Goto See;
		
		Death:
			NCYB J 10 A_NTM_Death;
			NCYB JJJJJJKLM 8 A_NTM_BossDeath;
			NCYB N 8 A_NoBlocking;
			NCYB OPQ 8;
			NCYB R 64;
			NCYB R -1 A_BossDeath;
			Stop;
	}
	
	action void A_NTM_CyberHoof() {
		A_StartSound("cyber/hoof", Chan_Body, ChanF_Overlap);
	}
	
	action void A_NTM_CyberMetal() {
		A_StartSound("cyber/metal", Chan_Body, ChanF_Overlap);
	}
	
	action void A_NTM_CyberAttack() {
		if (!target) {
			SetStateLabel("See");
			
			return;
		}
		
		A_FaceTarget();
		A_SpawnProjectile("NTM_Rocket", 48, -22.5);
	}
	
	action void A_NTM_CyberAttack2() {
		if (!target) {
			SetStateLabel("See");
			
			return;
		}
		
		A_FaceTarget();
		A_StartSound("cyber/attack", Chan_Weapon);
		A_SpawnProjectile("NTM_CyberdemonMissile", 48, 22.5);
	}
}

class NTM_CyberdemonMissile : NTM_Rocket {
	Default {
		Scale 0.666;
		Radius 4;
		Height 5;
		Speed 32;
		Translation "128:143=96:111", "144:151=104:111", "120:127=5:8";
		DeathSound "cyber/tracex";
		
		+SeekerMissile
	}
	
	States {
		Spawn:
			NMSL A 2 Bright A_SpawnItemEx("NTM_RocketSmokeTrail");
			NMSL A 0 A_Tracer2(3);
			Loop;
			
		Death:
			TNT1 A 0 {
				Actor boom = Spawn("NTM_Explosion2", pos);
				
				if (boom) {
					boom.scale *= 0.5;
				}
			}
			
			Stop;
	}
}

class NTM_Cyberdemon2 : NTM_Cyberdemon {
	States {
		Death:
			NCYB J 10 A_NTM_Death;
			NCYB JJJJJJKLM 8 A_NTM_BossDeath;
			NCYB N 8 A_NoBlocking;
			NCYB OPQ 8;
			NCYB R 64;
			NCYB R -1 A_KeenDie;
			Stop;
	}
}