class NTM_Player : DoomPlayer {
	int spawnTics, fallState, fallTics, fallTics2;
	double hNewAngle;
	
	Default {
		Player.StartItem "Pistol";
		Player.StartItem "Fist";
		Player.StartItem "Clip", 50;
		Player.WeaponSlot 1, "Fist", "Chainsaw";
		Player.WeaponSlot 2, "Pistol";
		Player.WeaponSlot 3, "Shotgun", "SuperShotgun";
		Player.WeaponSlot 4, "Chaingun";
		Player.WeaponSlot 5, "RocketLauncher";
		Player.WeaponSlot 6, "PlasmaRifle";
		Player.WeaponSlot 7, "BFG9000";
		Player.CrouchSprite "NPLC";
		Scale 0.666;
	}
	
	States {
		Spawn:
			NPLA A 1 A_PlayerJump;
			Loop;
		
		See:
			NPLA ABCD 4 A_PlayerJump;
			Loop;
		
		Jump:
			NPLA M 1 {
				if (CantJump()) {
					SetStateLabel("Spawn");
				}
				
				if (vel.z <= 0) {
					SetStateLabel("Fall");
				}
			}
			
			Loop;
		
		Fall:
			NPLA N 1 {
				if (pos.z <= floorZ || bOnMObj || CantJump()) {
					SetStateLabel("Spawn");
				}
				
				if (vel.z > 0) {
					SetStateLabel("Jump");
				}
			}
			
			Loop;
		
		Missile:
			NPLA E 12;
			Goto Spawn;
		
		Melee:
			NPLA F 6 Bright;
			Goto Missile;
		
		Pain:
			NPLA G 4;
			NPLA G 4 A_Pain;
			Goto Spawn;
		
		Death:
			NPLA H 7;
			NPLA I 7 A_PlayerScream;
			NPLA J 7 A_NoBlocking;
			NPLA K 7;
			NPLA L -1;
			Stop;
		
		XDeath:
			NMGI A 1 A_XScream;
			NMGI A 4 A_SkullPop("NTM_PlayerHead");
			NMGI B 5 A_NoBlocking;
			NMGI CDEFG 5;
			NMGI H -1;
			Stop;
	}
	
	void A_PlayerJump() {
		if (!CantJump()) {
			double velZ = vel.z;
			
			if (velZ > 0) {
				SetStateLabel("Jump");
			}
			
			if (velZ < 0) {
				SetStateLabel("Fall");
			}
		}
	}
	
	bool CantJump() {
		return (bNoClip || bFly || waterLevel > 0);
	}
	
	override void Tick() {
		// Less slippery movement
		if (!multiplayer && curSector && !(curSector.flags & 16)) {
			level.airControl = 0.53777778;
			A_SetSpeed(2.24);
			vel.xy *= 0.88;
			viewBob = 0.88;
		} else {
			level.airControl = 0.00390625;
			A_SetSpeed(1);
		}
		
		// Flinch when falling
		if (!waterLevel && !bFly && spawnTics-- <= 0) {
			switch (fallState) {
				case (0): // Falling
					if (vel.z < 0) {
						if (fallTics < 11) {
							++fallTics;
						} else {
							fallState = 1;
						}
					} else {
						fallTics = 0;
					}
				break;
				
				case (1): // Thud
					if (pos.z <= floorZ || bOnMObj) {
						A_SetPitch(pitch + 15, SPF_Interpolate);
						fallState = 2;
						fallTics = 0;
						fallTics2 += 10;
					}
				break;
				
				case (2): // Flinch
					if (fallTics2 > 0) {
						A_SetPitch(pitch - 1.5, SPF_Interpolate);
						--fallTics2;
					} else {
						fallState = 0;
					}
				break;
			}
		}
		
		Super.Tick();
	}
	
	override Vector2 BobWeapon(double ticFrac) {
		if (multiplayer) {
			return Super.BobWeapon(ticFrac);
		}
		
		Vector2 p[2];
		Weapon w = player ? player.readyWeapon : null;
		
		if (!w || w.bDontBob) {
			return (0, 0);
		}
		
		for (int i = 0; i < 2; i++) {
			p[i] = (0, 0);
			
			let angle = w.bobSpeed * 128 * player.GetWBobSpeed() * (level.mapTime - 1 + i) * 360 * 0.0001220703125;
			double hAngle = (player.vel.Length() > 0) ? (3, 3) dot (3, 3) : 0;
			
			hNewAngle = NTMAI.Lerp(hNewAngle, hAngle, 0.1);
			p[i].x = Round(hNewAngle * w.bobRangeX * viewBob);
			p[i].y = Round(player.bob * ((hAngle * 0.0625) * w.bobRangeY * viewBob));
			p[i].x *= Cos(angle);
			p[i].y *= Abs(Sin(angle));
		}
		
		return (p[0] * (1 - ticFrac) + p[1] * ticFrac);
	}
}

class NTM_PlayerHead : PlayerChunk {
	Default {
		Scale 0.625;
		Gravity 0.3;
		BounceType "Doom";
		Radius 4;
		Height 4;
		
		+CanBounceWater
		+Missile
		+UseBounceState
	}
	
	States {
		Spawn:
			NHEA A 0 NoDelay {
				viewBob = 0;
				player.attacker = null;
				scale.x *= FRandomPick(-1, 1);
			}
			
			NHEA ABCD 3 A_JumpIf(pos.z <= floorZ && vel.z == 0, "Death");
			Goto Spawn + 2;
		
		Bounce.Floor:
			NHEA A 0 {
				A_SpawnItem("NTM_FlyingBlood");
				
				if (Abs(vel.z) >= 3) {
					A_StartSound("NTM_FlyingGib/splat");
				}
			}
			
			Goto Spawn + 1;
		
		Death:
			NHEA E 16 A_CheckPlayerDone;
			Wait;
	}
}