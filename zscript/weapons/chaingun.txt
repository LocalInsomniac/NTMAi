class NTM_ChaingunSpawner : NTM_ItemSpawner replaces Chaingun {
	Default {
		NTM_ItemSpawner.Type "NTM_Chaingun";
	}
	
	override void UpdateType(NTMAi e) {
		if ((sv_ntm_r_custom && (e.bADMORTEM || e.bMAYhem2019)) || (sv_ntm_playerRandom == 1 && Random(0, 1))) {
			type = "NTM_TommyGun";
		}
	}
}

class NTM_Chaingun : Chaingun {
	int fractionTic;
	
	Default {
		Scale 0.8;
		Weapon.SlotNumber 4;
		Weapon.UpSound "NTM_Weapon/select";
		Inventory.PickupMessage "Chaingun!";
	}
	
	States {
		Spawn:
			NCHA F -1;
			Stop;
			
		Ready:
			NCHA A 1 A_WeaponReady;
			Loop;
		
		Deselect:
			NCHA A 1 A_Lower(12);
			Loop;
		
		Select:
			NCHA A 1 A_Raise(12);
			Loop;
		
		Fire:
			NCHA E 1 Bright {
				A_StartSound("weapons/chngun", Chan_Weapon);
				A_FireBullets(5.6, 0, 1, 5, "BulletPuff");
				A_GunFlash();
				A_WeaponOffset(Random(0, 8), Random(36, 40), WOF_Interpolate);
				invoker.fractionTic = (invoker.fractionTic + 1) % 3;
			}
			
			NCHA B 1 {
				A_SpawnItemEx("NTM_BulletCasing", -4, 25, 21, FRandom(-7, -5), 0, FRandom(5, 7), 90);
				A_WeaponOffset(Random(-2, 4), Random(34, 38), WOF_Interpolate);
			}
			
			NCHA B 0 A_JumpIf(invoker.fractionTic, invoker.fractionTic);
			NCHA CD 1 A_WeaponOffset(Random(-2, 2), Random(32, 34), WOF_Interpolate);
			NCHA AA 1 A_Refire;
			
			NCHA B 1 {
				A_WeaponOffset(flags: WOF_Interpolate);
				A_Refire();
			}
			
			NCHA BBCCCCDDDDD 1 A_Refire;
			Goto Ready;
		
		Flash:
			TNT1 A 1 A_Light2;
			Goto LightDone;
	}
}