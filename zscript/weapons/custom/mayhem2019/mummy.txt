class NTM_MummyWeapon : Fist {
	int attacks;
	
	Default {
		Tag "Mummy Hands";
		Weapon.SlotNumber 1;
		Inventory.AltHUDIcon "NMUWA0";
	}
	
	States {
		Ready:
			NMUW A 1 A_WeaponReady;
			Loop;
			
		Deselect:
			NMUW A 1 A_Lower(12);
			Loop;
			
		Select:
			NMUW A 1 A_Raise(12);
			Loop;
		
		Fire:
			NMUW A 0 A_JumpIf(CountInv("NTM_MummyShots") >= 3, "Fire2");
			NMUW A 0 A_GiveInventory("NTM_MummyShots", 1);
			NMUW A 2 A_WeaponOffset(8, 32, WOF_Interpolate);
			NMUW A 2 A_WeaponOffset(64, 16, WOF_Interpolate);
			NMUW B 2 A_WeaponOffset(128, -4, WOF_Interpolate);
			NMUW B 2 A_WeaponOffset(136, -6, WOF_Interpolate);
			NMUW C 2 A_WeaponOffset(0, 30, WOF_Interpolate);
			NMUW C 2 A_WeaponOffset(-48, 64, WOF_Interpolate);
			
			NMUW D 2 {
				A_FireProjectile("NTM_MummyBall", 0, false, 5);
				A_WeaponOffset(-64, 64, WOF_Interpolate);
			}
			
			NMUW D 2 A_WeaponOffset(-128, 128, WOF_Interpolate);
			NMUW D 2 A_WeaponOffset(-56, 80, WOF_Interpolate);
			NMUW D 2 A_WeaponOffset(-16, 64, WOF_Interpolate);
			NMUW A 2 A_WeaponOffset(-8, 48, WOF_Interpolate);
			NMUW A 0 A_WeaponOffset(0, 32, WOF_Interpolate);
			Goto Ready;
		
		Fire2:
			NMUW E 0 A_TakeInventory("NTM_MummyShots");
			NMUW E 2 Bright A_WeaponOffset(8, 32, WOF_Interpolate);
			NMUW E 2 Bright A_WeaponOffset(32, 16, WOF_Interpolate);
			NMUW F 2 Bright A_WeaponOffset(64, -4, WOF_Interpolate);
			NMUW F 5 Bright A_WeaponOffset(66, -6, WOF_Interpolate);
			NMUW C 2 Bright A_WeaponOffset(0, 30, WOF_Interpolate);
			NMUW C 2 Bright A_WeaponOffset(-16, 64, WOF_Interpolate);
			
			NMUW D 20 {
				A_FireProjectile("NTM_FlameAttack");
				A_WeaponOffset(-32, 128, WOF_Interpolate);
			}
			
			NMUW D 2 A_WeaponOffset(-128, 128, WOF_Interpolate);
			NMUW D 2 A_WeaponOffset(-56, 80, WOF_Interpolate);
			NMUW D 2 A_WeaponOffset(-16, 64, WOF_Interpolate);
			NMUW A 2 A_WeaponOffset(-8, 48, WOF_Interpolate);
			NMUW A 0 A_WeaponOffset(0, 32, WOF_Interpolate);
			Goto Ready;
	}
}

class NTM_MummyShots : Inventory {
	Default {
		+Inventory.Undroppable
		+Inventory.Untossable
		Inventory.MaxAmount 3;
	}
	
	States {
		Spawn:
			TNT1 A 0;
			Fail;
	}
}