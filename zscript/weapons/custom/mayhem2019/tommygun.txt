class NTM_TommyGun : NTM_Chaingun {
	Default {
		Weapon.SelectionOrder 699;
		Weapon.UpSound "NTM_TommyGun/in";
		Inventory.PickupMessage "Tommy Gun!";
		Obituary "%o was turned into swiss cheese by %k's Tommy Gun.";
		Tag "Tommy Gun";
	}
	
	States {
		Spawn:
			NTOM F -1;
			Stop;
			
		Ready:
			NTOM A 1 A_WeaponReady;
			Loop;
		
		Deselect:
			NTOM A 1 A_Lower(12);
			Loop;
		
		Select:
			NTOM A 1 A_Raise(12);
			Loop;
		
		Fire:
			NTOM A 0 A_Jump(128, "Fire2");
			NTOM B 2 Bright A_NTM_TommyGun;
			Goto FireEnd;
		
		Fire2:
			NTOM A 0 A_Jump(128, "Fire3");
			NTOM C 2 Bright A_NTM_TommyGun;
			Goto FireEnd;
		
		Fire3:
			NTOM A 0 A_Jump(128, "Fire4");
			NTOM D 2 Bright A_NTM_TommyGun;
			Goto FireEnd;
		
		Fire4:
			NTOM E 2 Bright A_NTM_TommyGun;
			Goto FireEnd;
		
		FireEnd:
			NTOM A 1 A_NTM_TommyGun2;
			NTOM A 0 A_Refire;
			Goto Ready;
		
		Flash:
			TNT1 A 0 A_Jump(128, 2);
			TNT1 A 2 A_Light1;
			Goto LightDone;
			TNT1 A 2 A_Light2;
			Goto LightDone;
	}
	
	action void A_NTM_TommyGun() {
		A_StartSound("NTM_TommyGun/fire", Chan_Weapon);
		A_FireBullets(5.6, 0, 1, 5, "BulletPuff");
		A_GunFlash();
		A_WeaponOffset(Random(0, 5), Random(32, 37), WOF_Interpolate);
	}
	
	action void A_NTM_TommyGun2() {
		A_SpawnItemEx("NTM_BulletCasing", -8, 26, 36, FRandom(4, 6), 0, FRandom(4, 6), 90);
		A_WeaponOffset(0, 32, WOF_Interpolate);
	}
}