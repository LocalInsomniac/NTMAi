class NTM_PlasmaRifle : PlasmaRifle {
	Default {
		Scale 0.75;
		Weapon.SlotNumber 6;
		Weapon.UpSound "NTM_Weapon/select";
		Inventory.PickupMessage "Plasma Rifle!";
	}
	
	States
	{
		Ready:
			NPLS A 1 A_WeaponReady;
			Loop;
			
		Deselect:
			NPLS A 1 A_Lower(12);
			Loop;
			
		Select:
			NPLS A 1 A_Raise(12);
			Loop;
			
		Fire:
			NPLS C 1 Bright {
				A_FireProjectile("NTM_PlasmaBall", 0, true, 7, 2);
				A_GunFlash();
				A_WeaponOffset(Random(-2, 2), Random(32, 36), WOF_Interpolate);
			}
			
			NPLS B 1 Bright A_WeaponOffset(Random(-2, 2), Random(32, 36), WOF_Interpolate);
			NPLS A 1 A_WeaponOffset(flags: WOF_Interpolate);
			NPLS D 5 A_Refire;
			NPLS EED 5;
			Goto Ready;
			
		Flash:
			TNT1 A 1 A_Light2;
			TNT1 A 1 A_Light1;
			Goto LightDone;
	}
}

class NTM_PlasmaBall : PlasmaBall {
	Default {
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			NPLB A 0 NoDelay A_Jump(128, 2);
			NPLB ABCD 1 Bright;
			Goto Spawn + 1;
			
		Death:
			NPLB EEFGHIJ 2 Bright;
			Stop;
	}
}