class NTM_Chainsaw : Chainsaw replaces Chainsaw {
	Default {
		Scale 0.75;
		Weapon.SlotNumber 1;
	}
	
	States {
		Select:
			NCSA A 0 A_JumpIfInventory("NTM_Bloody", 1, 2);
			NCSA A 1 A_Raise;
			Loop;
			NCSA E 1 A_Raise;
			Goto Select + 2;
			
		Deselect:
			NCSA A 0 A_JumpIfInventory("NTM_Bloody", 1, 2);
			NCSA A 1 A_Lower(12);
			Loop;
			NCSA E 1 A_Lower(12);
			Goto Deselect + 2;
			
		Ready:
			NCSA A 0 A_StartSound("weapons/sawidle", Chan_Weapon);
			NCSA A 0 A_JumpIfInventory("NTM_Bloody", 1, 3);
			NCSA AB 4 A_NTM_SawIdle;
			Loop;
			NCSA EF 4 A_NTM_SawIdle;
			Loop;
			
		Fire:
			NCSA C 0 A_JumpIfInventory("NTM_Bloody", 1, "Fire2");
			NCSA CD 3 A_NTM_Saw;
			NCSA D 0 A_Refire;
			Goto Ready;
		
		Fire2:
			NCSA GH 3 A_NTM_Saw;
			NCSA H 0 A_Refire("Fire2");
			Goto Ready;
	}
	
	action void A_NTM_SawIdle() {
		A_WeaponReady();
		A_WeaponOffset(0, Random(32, 34), WOF_Interpolate);
	}
	
	action void A_NTM_Saw() {
		A_Saw("weapons/sawfull", "weapons/sawhit", 3, "NTM_ChainsawPuff", SF_NoTurn);
		A_WeaponOffset(Random(-2, 2), Random(32, 38), WOF_Interpolate);
	}
}

class NTM_ChainsawPuff : NTM_BulletPuff {
	Default {
		DamageType "Chainsaw";
	}
	
	States {
		Spawn:
			PUFF A 4 Bright NoDelay A_StartSound("NTM_ChainsawPuff/miss");
			Goto Super::Spawn + 1;
	}
}

class NTM_Bloody : NTM_PistolShots {}