class NTM_RocketLauncherSpawner : Actor replaces RocketLauncher {
	override void Tick() {
		super.Tick();
		
		class<Actor> cls = "NTM_RocketLauncher";
		
		if (sv_ntm_r_custom) {
			NTMAi eh = NTMAi(EventHandler.Find("NTMAi"));
			
			if (eh && eh.bMAYhem2019) {
				cls = "NTM_NapalmLauncher";
			}
		}
		
		A_SpawnItemEx(cls, 0, 0, 0, vel.x, vel.y, vel.z, 0, SXF_AbsoluteVelocity | SXF_NoCheckPosition | SXF_TransferSpecial);
		Destroy();
	}
}

class NTM_RocketLauncher : RocketLauncher {
	Default {
		Scale 0.75;
		Weapon.SlotNumber 5;
		Weapon.UpSound "NTM_Weapon/select";
		Inventory.PickupMessage "Rocket Launcher!";
	}
	
	States {
		Ready:
			NROC A 1 A_WeaponReady;
			Loop;
			
		Deselect:
			NROC A 1 A_Lower(12);
			Loop;
			
		Select:
			NROC A 1 A_Raise(12);
			Loop;
			
		Fire:
			NROC B 3 Bright {
				A_FireProjectile("NTM_Rocket", 0, true, 5, -8);
				A_GunFlash();
				A_WeaponOffset(12, 50, WOF_Interpolate);
			}
			
			NROC C 3 Bright A_WeaponOffset(16, 56, WOF_Interpolate);
			NROC D 3 Bright A_WeaponOffset(18, 58, WOF_Interpolate);
			NROC A 3 A_WeaponOffset(12, 50, WOF_Interpolate);
			NROC A 3 A_WeaponOffset(4, 38, WOF_Interpolate);
			NROC A 3 A_WeaponOffset(flags: WOF_Interpolate);
			NROC A 0 A_Refire;
			Goto Ready;
		
		Flash:
			TNT1 A 3 A_Light1;
			TNT1 A 3 A_Light2;
			TNT1 A 3 A_Light1;
			Goto LightDone;
		
		Spawn:
			NROC E -1;
			Stop;
	}
}

class NTM_Rocket : Rocket replaces Rocket {
	Default {
		Radius 10;
		Speed 25;
		Scale 0.9;
		
		-ZDoomTrans
		-RocketTrail
	}
	
	States {
		Spawn:
			NMSL A 2 Bright A_SpawnItemEx("NTM_RocketSmokeTrail", 0, 0, 4);
			Loop;
			
		Death:
			TNT1 A 15 {
				Spawn("NTM_Explosion2", pos);
				NTMAi.Explode(self, 128, 128, XF_HurtSource);
				A_Quake(2, 15, 0, 512, "");
			}
			
			Stop;
	}
}

class NTM_RocketSmokeTrail : RocketSmokeTrail {
	Default {
		Scale 0.64;
		
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			RSMK ABCD 3;
			Stop;
	}
}

class NTM_RocketBox : CustomInventory replaces RocketBox {
	Default {
		Inventory.PickupMessage "Box of Explosives";
	}
	
	States {
		Spawn:
			BROK A -1;
			Stop;
			
		Pickup:
			TNT1 A 0 A_JumpIfInventory("RocketAmmo", 0, 2);
			
			TNT1 A 0 {
				A_GiveInventory("RocketAmmo", 5);
				
				if (Random(0, 1)) {
					A_GiveInventory("NTM_GrenadeItem", 1);
				}
			}
			
			Stop;
			TNT1 A 0;
			Fail;
	}
}

class NTM_RocketAmmoSpawner : Actor replaces RocketAmmo {
	override void Tick() {
		super.Tick();
		
		class<Actor> cls = "RocketAmmo";
		
		if (sv_ntm_r_custom) {
			NTMAi eh = NTMAi(EventHandler.Find("NTMAi"));
			
			if (eh && eh.bMAYhem2019) {
				cls = "NTM_NapalmAmmo";
			}
		}
		
		A_SpawnItemEx(cls, 0, 0, 0, vel.x, vel.y, vel.z, 0, SXF_AbsoluteVelocity | SXF_NoCheckPosition | SXF_TransferSpecial);
		Destroy();
	}
}