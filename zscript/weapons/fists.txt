class NTM_Fists : Fist {
	Default {
		Tag "Fists";
		Weapon.SlotNumber 1;
		Inventory.AltHUDIcon "NFISA0";
		
		+Weapon.NoAlert
	}
	
	States {
		Ready:
			TNT1 A 1 A_WeaponReady;
			Loop;
		
		Deselect:
			TNT1 A 0 A_Lower;
			Loop;
		
		Select:
			TNT1 A 0 A_Raise;
			Loop;
		
		Fire:
			NFIS AB 2;
			NFIS C 3 A_NTM_Punch;
			NFIS DBA 3;
			TNT1 A 0 A_Refire("Fire2");
			Goto Ready;
		
		Fire2:
			NFIS EF 2;
			NFIS G 3 A_NTM_Punch;
			NFIS HFE 3;
			TNT1 A 0 A_Refire;
			Goto Ready;
	}
	
	action void A_NTM_Punch() {
		if (CountInv("PowerStrength")) { // || CountInv("NTM_PowerBerserk")
			A_CustomPunch(20, false, CPF_NoTurn, "NTM_BerserkPuff", 66, 0.25, 100, "", "player/male/fist");
		} else {
			A_CustomPunch(2, false, CPF_NoTurn, "NTM_FistPuff", 66, 0, 0, "", "player/male/fist");
		}
	}
}

class NTM_FistPuff : NTM_BulletPuff {
	States {
		XDeath:
			TNT1 A 0;
			Stop;
	}
}

class NTM_BerserkPuff : NTM_FistPuff {
	Default {
		DamageType "Berserk";
	}
}