class NTM_SuperShotgun : SuperShotgun replaces SuperShotgun {
	Default {
		Scale 0.75;
		Weapon.SlotNumber 3;
		Weapon.UpSound "weapons/sshotc";
		Weapon.Kickback 100;
	}
	
	States {
		Ready:
			NSUP A 1 A_WeaponReady;
			Loop;
		
		Deselect:
			NSUP A 1 A_Lower(12);
			Loop;
		
		Select:
			NSUP A 1 A_Raise(12);
			Loop;
		
		Fire:
			NSUP A 2 Bright {
				A_FireBullets(11.2, 7.1, 20, 5, "NTM_SSGPuff");
				A_StartSound("weapons/sshotf", Chan_Weapon);
				A_GunFlash();
			}
			
			NSUP A 2 Bright A_WeaponOffset(20, 60, WOF_Interpolate);
			NSUP A 2 A_WeaponOffset(15, 55, WOF_Interpolate);
			NSUP A 2 A_WeaponOffset(8, 44, WOF_Interpolate);
			NSUP A 2 A_WeaponOffset(3, 36, WOF_Interpolate);
			NSUP B 3 A_WeaponOffset(flags: WOF_Interpolate);
			NSUP B 2 A_CheckReload;
			NSUP C 3;
			NSUP D 3 A_StartSound("weapons/sshoto", Chan_Weapon, ChanF_Overlap);
			
			NSUP E 3 {
				A_SpawnItemEx("NTM_ShellCasing", -5, 24, 32, FRandom(-4, -3), 0, FRandom(3, 5), 90);
				A_SpawnItemEx("NTM_ShellCasing", -2, 24, 32, FRandom(-4, -3), 0, FRandom(3, 5), 90);
			}
			
			NSUP F 4;
			NSUP G 4 A_StartSound("weapons/sshotl", Chan_Weapon, ChanF_Overlap);
			NSUP H 4;
			NSUP IC 3;
			NSUP B 5 A_StartSound("weapons/sshotc", Chan_Weapon, ChanF_Overlap);
			NSUP A 0 A_ReFire;
			Goto Ready;
		
		Flash:
			NSUP J 2 Bright A_Light1;
			NSUP K 2 Bright A_Light2;
			Goto LightDone;
		
		Spawn:
			SGN2 A -1;
			Stop;
	}
}

class NTM_SSGPuff : NTM_BulletPuff {
	Default {
		DamageType "SuperShotgun";
	}
}