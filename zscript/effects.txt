#include "zscript/effects/blood.txt"
#include "zscript/effects/splashes.txt"

// Explosion
class NTM_Explosion : Actor {
	Default {
		RenderStyle "Add";
		Translation "NTM_Explosion";
		FloatBobPhase 0;
		
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NoGravity
		+NotOnAutomap
		+ForceXYBillboard
		+ZDoomTrans
	}
	
	States {
		Spawn:
			NEXP ABCDEFGHIJ 3 Bright;
			Stop;
	}
}

// Explosion 2
class NTM_Explosion2 : NTM_Explosion {
	Default {
		Scale 0.75;
	}
	
	States {
		Spawn:
			NEX2 ABCDEFGHIJKLMNOPQRSTU 2 Bright;
			Stop;
	}
}

// Gib Base
class NTM_Gib : Actor {
	Default {
		FloatBobPhase 0;
		Health 10;
		Radius 4;
		Height 8;
		Mass 1;
		Gravity 0.5;
		BounceType "Doom";
		
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NotOnAutomap
		+BounceOnWalls
		+DontSplash
		+ThruActors
	}
	
	States {
		Spawn:
		Crash:
			TNT1 A 0;
			Stop;
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		scale.x *= FRandomPick(-1, 1);
		scale.y *= FRandomPick(-1, 1);
	}
}

// Barrel Gib
class NTM_BarrelGib : NTM_Gib {
	Default {
		Scale 0.25;
		
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			NBGB ABCDEFGH 3;
			Loop;
	}
}

// Bullet Puff
class NTM_BulletPuff : BulletPuff replaces BulletPuff {
	Default {
		Scale 0.8;
		FloatBobPhase 0;
		
		+PuffOnActors
		+NotOnAutomap
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			PUFF A 4 Bright NoDelay A_StartSound("NTM_BulletPuff/miss");
			PUFF BCD 4;
			Stop;
			
		Melee:
			PUFF C 4 A_StartSound("player/male/fist");
			PUFF D 4;
			Stop;
		
		XDeath:
			TNT1 A 0 A_StartSound("NTM_BulletPuff/hit");
			Stop;
    }
}

// Bullet Casing
class NTM_BulletCasing : Actor {
	Default {
		FloatBobPhase 0;
		Scale 0.16;
		Radius 1;
		Height 1;
		Gravity 1;
		BounceSound "NTM_BulletCasing/bounce";
		BounceType "Doom";
		
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NotOnAutomap
		+CanBounceWater
		+DontSplash
		+Missile
		+UseBounceState
		+ForceXYBillboard
		+ThruActors
	}
	
	States
	{
		Spawn:
			NBUL A 0;
		Roll:
			#### ABCDEFGH 2;
			Loop;
			
		Bounce:
			#### A 0;
			Goto Roll;
			
		Death:
			#### C 350 A_Jump(128, 1);
			Stop;
			#### G 350;
			Stop;
	}
}

// Shell Casing
class NTM_ShellCasing : NTM_BulletCasing {
	Default {
		BounceSound "NTM_ShellCasing/bounce";
	}
	
	States {
		Spawn:
			NSHE A 0;
			Goto Roll;
	}
}