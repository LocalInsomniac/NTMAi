#include "zscript/effects/blood.txt"
#include "zscript/effects/casings.txt"
#include "zscript/effects/explosions.txt"

// Gib Base
class NTM_Gib : Actor {
	private int gibFlags;
	flagDef NoRandomFlip: gibFlags, 0;
	
	Default {
		FloatBobPhase 0;
		Health 10;
		Radius 4;
		Height 8;
		Mass 1;
		Gravity 0.5;
		BounceType "Doom";
		
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NotOnAutomap
		+BounceOnWalls
		+DontSplash
		+ThruActors
		+FloorClip
	}
	
	States {
		Spawn:
		Crash:
			TNT1 A 0;
			Stop;
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		
		if (!bNoRandomFlip) {
			bXFlip = Random(0, 1);
			bYFlip = Random(0, 1);
		}
	}
}

// Bullet Puff
class NTM_BulletPuff : BulletPuff replaces BulletPuff {
	Default {
		Height 4;
		Scale 0.666;
		FloatBobPhase 0;
		
		+PuffOnActors
		+NotOnAutomap
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			NPUF A 2 Bright NoDelay A_StartSound("NTM_BulletPuff/miss");
			NPUF BCDEF 2;
			Stop;
			
		Melee:
			NPUF C 2 A_StartSound("player/male/fist");
			NPUF DEF 2;
			Stop;
		
		XDeath:
			TNT1 A 0 A_StartSound("NTM_BulletPuff/hit");
			Stop;
    }
}

// Teleport Fog
class NTM_TeleportFog : Actor replaces TeleportFog {
	Default {
		Height 32;
		Scale 0.622;
		Speed 0.08;
		RenderStyle "Add";
		SeeSound "misc/teleport";
		Translation "NTM_Fog_Green";
		
		+NoBlockMap
		+NoTeleport
		+NoGravity
		+ZDoomTrans
		+ForceXYBillboard
	}
	
	States {
		Spawn:
			NFOG AB 2 Bright;
			Loop;
	}
	
	override void PostBeginPlay() {
		super.PostBeginPlay();
		
		AddZ(height, false);
		A_StartSound(seeSound);
	}
	
	override void Tick() {
		super.Tick();
		
		scale += (-speed, speed);
		
		if (scale.x <= 0) {
			Destroy();
		}
	}
}

// Item Fog
class NTM_ItemFog : NTM_TeleportFog replaces ItemFog {
	Default {
		Height 12;
		Scale 0.411;
		Speed 0.05;
		SeeSound "";
		Translation "NTM_Fog_Blue";
	}
}

// Morph Fog
class NTM_MorphFog : NTM_TeleportFog {
	Default {
		Translation "NTM_Explosion_Archvile";
	}
}