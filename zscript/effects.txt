#include "zscript/effects/blood.txt"
#include "zscript/effects/splashes.txt"

// Explosion
class NTM_Explosion : Actor {
	Default {
		RenderStyle "Add";
		Translation "NTM_Explosion";
		FloatBobPhase 0;
		
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NoGravity
		+NotOnAutomap
		+ForceXYBillboard
		+ZDoomTrans
	}
	
	States {
		Spawn:
			NEXP ABCDEFGHIJ 3 Bright;
			Stop;
	}
}

// Explosion 2
class NTM_Explosion2 : NTM_Explosion {
	Default {
		Scale 0.75;
		
		-ForceXYBillboard
	}
	
	States {
		Spawn:
			NEX2 ABCDEFGHIJKLMNOPQRSTU 2 Bright;
			Stop;
	}
}

// Gib Base
class NTM_Gib : Actor {
	Default {
		FloatBobPhase 0;
		Health 10;
		Radius 4;
		Height 8;
		Mass 1;
		Gravity 0.5;
		BounceType "Doom";
		
		+Corpse
		+NoTeleport
		+NoBlockMap
		+NotOnAutomap
		+BounceOnWalls
		+DontSplash
		+ThruActors
	}
	
	States {
		Spawn:
		Crash:
			TNT1 A 0;
			Stop;
	}
	
	override void PostBeginPlay() {
		Super.PostBeginPlay();
		scale.x *= FRandomPick(-1, 1);
		scale.y *= FRandomPick(-1, 1);
	}
}

// Barrel Gib
class NTM_BarrelGib : NTM_Gib {
	Default {
		Scale 0.25;
	}
	
	States {
		Spawn:
			NBGB ABCDEFGH 3;
			Loop;
	}
}

// Bullet Puff
class NTM_BulletPuff : BulletPuff replaces BulletPuff {
	Default {
		Scale 0.8;
		FloatBobPhase 0;
		
		+PuffOnActors
		+NotOnAutomap
	}
	
	States {
		Spawn:
			PUFF A 4 Bright NoDelay A_StartSound("NTM_BulletPuff/miss");
			PUFF B 4;
		Melee:
			PUFF CD 4;
			Stop;
		
		XDeath:
			TNT1 A 0 A_StartSound("NTM_BulletPuff/hit");
			Stop;
    }
}